#!/usr/bin/env stap++

@use nginx.chain
@use nginx.request
@use nginx.upstream

global size

probe @pfunc(ngx_http_lua_post_subrequest)
{
    $*data := @cast($data, "ngx_http_lua_post_subrequest_data_t")
    if (!$*data->ctx->seen_last_for_subreq) {
        r = $r
        $*r := @cast(r, "ngx_http_request_t")
        size += ngx_chain_buf_size($*data->ctx->body)
        printf("%s %s?%s: host: %s, ua: %s body size: %d, content-length: %d, cached: %d\n",
               ngx_req_method(r), ngx_req_uri(r), ngx_req_args(r),
               ngx_req_host(r), ngx_req_ua(r),
               size, $*r->headers_out->content_length_n, $*r->cached)
        u = $*r->upstream
        $*u := @cast(u, "ngx_http_upstream_t")
        if (u) {
            printf("  upstream resp-time=%s (ms), resp-len=%s (bytes), status=%s, addr=%s\n",
                   ngx_u_resp_times(r), ngx_u_resp_lens(r),
                   ngx_u_resp_statuses(r), ngx_u_addr(r))
        }
    }
}

probe begin {
    printf("Tracing %d ($^exec_path)\n\n", target())
}

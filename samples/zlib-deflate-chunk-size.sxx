#!/usr/bin/env stap++

/*
 * Copyright (C) Yichun Zhang (agentzh)
 */

global stats

probe @pfunc(deflate)!,
      process("$^libz_path").function("deflate")
{
    //warn("HERE")
    stats <<< $strm->avail_in
}

probe @pfunc(deflateEnd)!,
      process("$^libz_path").function("deflateEnd")
{
    //warn("HERE")
    stats <<< $strm->avail_in
}


probe begin {
    printf("Start tracing process %d ($^exec_path)...\n", target())
    %( "$^arg_time :default()" != ""  %?
    printf("Please wait for $^arg_time seconds...\n")
    %:
    printf("Hit Ctrl-C to end.\n")
    %)
}

%( "$^arg_time" != ""  %?
probe timer.s($^arg_time) {
    exit()
}
%)

probe end {
    count = @count(stats)
    if (count == 0) {
        printf("\nNo samples found so far.\n")

    } else {
        printf("\nDistribution of zlib deflate chunk sizes (in bytes) for %d samples:\n",
               count)
        printf("(min/avg/max: %d/%d/%d)\n", @min(stats), @avg(stats),
               @max(stats))
        print(@hist_log(stats))
    }
}

#!/usr/bin/env stap++

# Copyright (C) Yichun Zhang

@use nginx.pool

global stats

probe @pfunc(ngx_http_free_request).callee("ngx_destroy_pool")
{
    //printf("%p\n", $pool)
    stats <<< ngx_pool_size($pool)
}

probe end {
    count = @count(stats)
    if (count == 0) {
        printf("\nNo samples found so far.\n")

    } else {
        printf("\nDistribution of nginx request pool sizes for %d samples:\n",
               count)
        printf("(min/avg/max: %d/%d/%d)\n", @min(stats), @avg(stats),
               @max(stats))
        print(@hist_log(stats))
    }
}

probe begin {
    printf("Start tracing process %d ($^exec_path)...\n", target())
    %( "$^arg_time :default()" != ""  %?
    printf("Please wait for $^arg_time seconds...\n")
    %:
    printf("Hit Ctrl-C to end.\n")
    %)
}

%( "$^arg_time" != ""  %?
probe timer.s($^arg_time) {
    exit()
}
%)

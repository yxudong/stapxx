#!/usr/bin/env stap++

/*
 * Copyright (C) Yichun Zhang (agentzh)
 */

global active_reqs
global recv_begin
global queries
global sending

probe @pfunc(ngx_http_process_request)
{
    sending = 0
    active_reqs[$r] = 1
}

probe @pfunc(ngx_http_lua_socket_udp_send)
{
    sending = 1
}

probe @pfunc(ngx_unix_send)
{
    if (sending) {
        //warn(sprintf("HERE: %d, %.*s", $size, $size, user_string_n_quoted($buf, $size)))
        sending = 0
        size = $size
        query = ""
        for (i = 0; i < size; i++) {
            if ((i + 1) % 10 == 0) {
                query .= sprintf(" %02x\n", $buf[i])
            } else {
                query .= sprintf(" %02x", $buf[i])
            }
        }
        queries[$c] = query
    }
}

probe @pfunc(ngx_http_lua_socket_udp_read)
{
    r = $r
    u = $u
    sending = 0
    if (active_reqs[r]) {
        if (!recv_begin[r, u]) {
            recv_begin[r, u] = gettimeofday_us()
        }
    }
}

probe @pfunc(ngx_http_lua_socket_udp_finalize)
{
    sending = 0
    delete queries[$u->udp_connection->connection]
}

probe @pfunc(ngx_http_log_request)
{
    sending = 0
    delete active_reqs[$r]
}

probe @pfunc(ngx_http_lua_socket_udp_receive_retval_handler)
{
    sending = 0
    r = $r
    u = $u
    begin = recv_begin[r, u]
    if (begin) {
        elapsed = gettimeofday_us() - begin
        if (elapsed >= $^arg_threshold :default(1000000)) {
            printf("u->ft_type: %d, elapsed: %dus\n", $u->ft_type, elapsed)
            query = queries[$u->udp_connection->connection]
            printf("Query bytes (hex dump):\n%s\n", query)
            exit()
        }
    }
    delete recv_begin[r, u]
}

probe begin {
    printf("Start tracing process %d ($^exec_path)...\n", target())
    printf("Hit Ctrl-C to end.\n")
}

